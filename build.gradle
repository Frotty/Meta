buildscript {
	repositories {
		//mavenLocal()
		mavenCentral {
			content {
				includeGroupByRegex "org\\.jetbrains\\..*"
				includeGroup "org.jetbrains"
				includeGroup "com.google.code.gson"
				includeGroup "com.google.code.findbugs"
				includeGroup "com.google.errorprone"
				includeGroup "com.google.j2objc"
				includeGroup "com.google.guava"
				includeGroup "de.undercouch"
				includeGroup "com.github.gundy"
				includeGroup "org.antlr"
				includeGroup "org.checkerframework"
				includeGroup "net.java.dev.jna"
				// See https://github.com/autonomousapps/dependency-analysis-android-gradle-plugin/issues/352
//				includeGroup "com.squareup.okio"
//				includeGroup "com.github.ben-manes.caffeine"
//				includeGroup "com.squareup.moshi"
//				includeGroup "com.autonomousapps"
				// required by jitpack
				includeGroup "org.sonatype.oss"
			}
		}
	}
	dependencies {
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
		// See https://github.com/autonomousapps/dependency-analysis-android-gradle-plugin/issues/352
//		classpath "com.autonomousapps:dependency-analysis-gradle-plugin:$dependencyAnalysis"
	}
}

allprojects {
	apply plugin: 'eclipse'
	apply plugin: 'idea'
	// See https://github.com/autonomousapps/dependency-analysis-android-gradle-plugin/issues/352
//	apply plugin: "com.autonomousapps.dependency-analysis"
	apply plugin: 'maven-publish'
	ext.appName = 'Meta'

	configurations.all {
		exclude group: "com.badlogicgames.gdx", module: "gdx-backend-lwjgl"
	}
}

// If we add android exclude the project here
configure(subprojects) {
	apply plugin: 'kotlin'
	sourceCompatibility = JavaVersion.VERSION_1_8
	publishing {
		publications {
			mavenJava(MavenPublication) {
				from components.java
			}
		}
	}
}

subprojects {
	group = 'com.github.Frotty'
	version = '1.0.0-SNAPSHOT'

	repositories {
		//mavenLocal()
		mavenCentral {
			content {
				includeGroupByRegex "com\\.badlogicgames\\..*"
				includeGroupByRegex "org\\.jetbrains\\..*"
				includeGroup "org.jetbrains"
				includeGroup "com.kotcrab.vis"
				includeGroup "org.slf4j"
				includeGroup "org.apache.commons"
				includeGroup "ch.qos.logback"
				includeGroupByRegex "org\\.junit\\..*"
				includeGroup "org.junit"
				includeGroup "org.apiguardian"
				includeGroup "org.opentest4j"
				includeGroup "org.lwjgl"
				includeGroup "org.jcraft"
				includeGroup "net.java.dev.jna"
				// required by jitpack
				includeGroup "org.sonatype.oss"
				includeGroup "org.apache"
			}
		}
		maven {
			url "https://oss.sonatype.org/content/repositories/snapshots/"
			content {
				includeGroup "com.badlogicgames.gdx"
			}
		}
		maven {
			url 'https://jitpack.io'
			content {
				includeGroup "com.github.julman99"
			}
		}
	}

	tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
		kotlinOptions.jvmTarget = "1.8"
		kotlinOptions {
			freeCompilerArgs += "-progressive"
			freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
			freeCompilerArgs += "-Xinline-classes"
			freeCompilerArgs += "-Xjvm-default=all"
		}
	}
}

eclipse.project.name = 'MetaRuntime' + '-parent'

// See https://github.com/autonomousapps/dependency-analysis-android-gradle-plugin/issues/352
//dependencyAnalysis {
//	issues {
//		// configure for all projects
//		all {
//			onUnusedDependencies {
//				exclude('com.badlogicgames.gdx-controllers:gdx-controllers-desktop')
//			}
//		}
//	}
//}
